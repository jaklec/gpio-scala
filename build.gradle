apply plugin: 'scala'
apply plugin: 'idea'
apply plugin: 'maven'

group = "se.jaklec"
version = "$version"

sourceCompatibility = JavaVersion.VERSION_1_7

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
	provided
    testCompile.extendsFrom provided
}

sourceSets {
	main.compileClasspath += configurations.provided
	test.compileClasspath += configurations.provided
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.useAnt = false
}

task scalatest << {
    ant.taskdef(name: 'scalatest', classname: 'org.scalatest.tools.ScalaTestAntTask', classpath: sourceSets.test.runtimeClasspath.asPath)
    ant.scalatest(runpath: sourceSets.test.output.classesDir, haltonfailure: 'false', fork: 'false') {
        reporter(type: 'stderr')
    }
}
test.finalizedBy scalatest

dependencies {
	compile "org.scala-lang:scala-library:$scalaVersion"
    testCompile "org.scalatest:scalatest_2.10:$scalatestVersion"
}

plugins.withType(IdeaPlugin) {
    idea {
        module {
            scopes.PROVIDED.plus += configurations.provided
            scopes.COMPILE.minus += configurations.provided
            scopes.TEST.minus += configurations.provided
            scopes.RUNTIME.minus += configurations.provided
        }
    }
}

